/*
 * Copyright 2014-2018 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'nebula.plugin-plugin' version '9.2.4'
    id 'nebula.kotlin' version '1.3.20'
}

description 'Provides the Kotlin plugin via the Gradle plugin portal and allows Kotlin library versions to be omitted'

contacts {
    'dmthomas@gmail.com' {
        moniker 'Danny Thomas'
        github 'DanielThomas'
    }
    'roberto@perezalcolea.info' {
        moniker 'Roberto Perez Alcolea'
        github 'rpalcolea'
    }
}

repositories {
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
}

ext {
    kotlinVersion = '1.3.21'
}

// Dependencies are managed by the above property, we don't want to accidentally lock dependencies and publish out of step with the upstream Kotlin plugin
ext.set("dependencyLock.ignore", "true")

dependencies {
    compile "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
}

pluginBundle {
    plugins {
        kotlin {
            id = 'nebula.kotlin'
            displayName = 'Nebula Kotlin plugin'
            description = project.description
            tags = ['nebula', 'kotlin']
        }
    }
}

//TODO: remove this once https://youtrack.jetbrains.net/issue/KT-26808 is fixed
test {
    systemProperties = [
            ignoreDeprecations: true
    ]
}
